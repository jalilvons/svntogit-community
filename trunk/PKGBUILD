# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: larte <lauri.arte@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: gun1x <gunix@gunix.cloud>

pkgname=kubectl
pkgdesc="Kubernetes.io client binary"
pkgver=1.19.4
pkgrel=1
arch=('x86_64')
url="https://kubernetes.io"
license=('Apache')
depends=('glibc')
makedepends=('go' 'rsync' 'git')
source=(https://github.com/kubernetes/kubernetes/archive/v${pkgver}/kubernetes-${pkgver}.tar.gz
        gotags.patch)
sha512sums=('b5a2cd73db3b1454c54d500d21630c971b5dae94377180e658638384ef2b243f0b41f1ce00eb425ab5958bddbdc0c792be4f14b9b61a364a90992860ca9109da'
            '41a3ac1b849a8cfd824b21f5f3c0344a5b2342c51d33f2a79d3d25731a903e9338864faca495285868669be6926d7a9ce44febf40ecd2d40dec7cb1d79cb40b7')
b2sums=('a3402abbd524ba135b7fa5ae9451743da44e7691e9b646de01b265078d89099440e7d14b56b21f4b2f0e105825d51c9565b62d7ae81b644f2a0a0dde4f40122a'
        '9ccfe1fcee65e414bba3de3000c1c444590a662259d194c4c6b9fe4ad03dbd687350d7ee1f750b39224f298304eb0b18daa965cde2fcd3bce42e8c903062c30b')

prepare() {
  cd kubernetes-${pkgver}
  patch -Np1 < ../gotags.patch
  sed -E 's/(CGO_ENABLED)=0/\1=1/g' -i hack/lib/golang.sh
}

build() {
  cd kubernetes-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  export GOLDFLAGS="-linkmode=external"

  make WHAT=cmd/kubectl
  hack/update-generated-docs.sh
}

package() {
  cd kubernetes-${pkgver}
  install -Dm755 _output/bin/kubectl -t "$pkgdir/usr/bin"
  install -Dm644 docs/man/man1/kubectl* -t "$pkgdir/usr/share/man/man1"

  "$pkgdir/usr/bin/kubectl" completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/kubectl"
  "$pkgdir/usr/bin/kubectl" completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_kubectl"
}

# vim: ts=2 sw=2 et:
